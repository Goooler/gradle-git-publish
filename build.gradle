buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.ajoberstar:semver-vcs-gradle-grgit:0.1.0'
    }
}

plugins {
    id 'groovy'
    id 'org.sonarqube' version '2.2.1'
    id 'maven-publish'
    id 'org.ajoberstar.bintray' version '0.1.0'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'org.ajoberstar.stutter' version '0.1.0'
}

apply plugin: 'org.ajoberstar.semver-vcs-grgit'

group = 'org.ajoberstar'

sonarqube {
    properties {
        property 'sonar.projectVersion', version.toString().split('-')[0]
    }
}

stutter {
    supports '3.0', '3.1', '3.2', '3.2.1'
}

model {
    bintray {
        owner = 'ajoberstar'
        repo = 'maven'
        pkg = 'gradle-git-publish'
    }
}

repositories {
    jcenter()
}

configurations.all {
    exclude group: 'org.codehaus.groovy'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar:grgit:1.7.3-rc.1'

    compatTestCompile gradleTestKit()
    compatTestCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    compatTestCompile 'org.ajoberstar:grgit:1.7.3-rc.1'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from tasks.javadoc.outputs.files
}

publishing {
    repositories {
        mavenLocal()
    }
}

pluginBundle {
    website = 'https://github.com/ajoberstar/gradle-git-publish'
    vcsUrl = 'https://github.com/ajoberstar/gradle-git-publish.git'
    description = 'Publishing to Git repositories'
    plugins {
        publishPlugin {
            id = 'org.ajoberstar.git-publish'
            displayName = 'Git Publish Plugin'
            tags = ['publish', 'git']
        }
    }
    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

wrapper {
    gradleVersion = '3.2'
}
