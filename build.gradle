plugins {
  id 'org.ajoberstar.defaults' version '0.11.0'
  id 'groovy'
  id 'java-gradle-plugin'
  // TODO remove when Gradle dependency locking is available
  id 'nebula.dependency-lock' version '5.0.4'
}

group = 'org.ajoberstar'

repositories {
  jcenter()
}

dependencies {
  // gradle
  compile gradleApi()

  // groovy
  compile localGroovy()

  // grgit
  def grgitVersion = '[2.0.0,)'
  compile "org.ajoberstar:grgit:$grgitVersion"
  compatTestCompile "org.ajoberstar:grgit:$grgitVersion"

  // util
  compile 'com.google.guava:guava:[25.1-jre,)'

  // testing
  compatTestCompile gradleTestKit()
  compatTestCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
}

stutter {
  includeActiveRc = true
  java(8) {
    compatibleRange '4.3'
  }
}

publishing {
  repositories {
    maven {
      name = 'bintray'
      url = 'https://api.bintray.com/maven/ajoberstar/maven/gradle-git-publish/;publish=1'
      credentials {
        username = System.env['BINTRAY_USER']
        password = System.env['BINTRAY_KEY']
      }
    }
  }
}

pluginBundle {
  website = 'https://github.com/ajoberstar/gradle-git-publish'
  vcsUrl = 'https://github.com/ajoberstar/gradle-git-publish.git'
  description = 'Publishing to Git repositories'
  plugins {
    publishPlugin {
      id = 'org.ajoberstar.git-publish'
      displayName = 'Git Publish Plugin'
      tags = ['publish', 'git']
    }
  }
  mavenCoordinates {
    groupId = project.group
    artifactId = project.name
    version = project.version
  }
}
